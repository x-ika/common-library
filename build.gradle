//---------------------------------------------------------------------------------------
// PROJECT

group 'com.simplejcode.commons'
version '1.0'

//---------------------------------------------------------------------------------------
// IDEA

apply plugin: 'idea'

idea {
    module {
        excludeDirs = [file(".gradle"), file(".build"), file(".idea"), file(".out")]
    }
}

//---------------------------------------------------------------------------------------
// ALL

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    version '1.0'

    def sub = projectDir.path.substring(rootProject.projectDir.path.length())
    buildDir = new File(rootProject.projectDir, '.build/' + sub)

    //-----------------------------------------------------------------------------------
    // IDEA

    apply plugin: 'idea'
    idea {
        module {
            outputDir = new File(rootProject.projectDir, '.out')
            testOutputDir = new File(rootProject.projectDir, '.out')
        }
    }

    //-----------------------------------------------------------------------------------
    // JAVA

    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        sourceCompatibility = '10'
        targetCompatibility = '10'
    }

    //-----------------------------------------------------------------------------------
    // PUBLISH

    apply plugin: 'maven-publish'

    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    //-----------------------------------------------------------------------------------
    // DEPENDENCIES

    configurations {

        // test dependencies
        junit

        // java ee api libraries
        javaxXmlBind
        mail
        ws
        ejb

        // java ee implementation libraries
        hibernate

        // third party libraries
        log4j
        lombok
        apacheConfiguration
        gson
        jsoniter
        gson
        poi
        googleTwoFA
        googleQrCode

    }

    dependencies {

        junit                          'junit:junit:4.12'

        javaxXmlBind                   'javax.xml.bind:jaxb-api:2.1'
        mail                           'javax.mail:mail:1.4.3'
        mail                           'javax.mail:mailapi:1.4.3'
        ws                             'javax.websocket:javax.websocket-api:1.1'
        ejb                            'javax.ejb:javax.ejb-api:3.2.2'

        hibernate                      'org.hibernate:hibernate-core:5.3.1.Final'

        log4j                          'log4j:log4j:1.2.17'
        lombok                         'org.projectlombok:lombok:1.18.8'
        annotationProcessor            ('org.projectlombok:lombok:1.18.8')
        apacheConfiguration            'org.apache.commons:commons-configuration2:2.0'
        gson                           'com.google.code.gson:gson:2.8.5'
        jsoniter                       'com.jsoniter:jsoniter:0.9.23'
        gson                           'com.google.code.gson:gson:2.8.6'
        poi                            'org.apache.poi:poi:4.0.1'
        poi                            'org.apache.poi:poi-ooxml:4.0.1'
        googleTwoFA                    'com.warrenstrange:googleauth:1.2.0'
        googleQrCode                   'com.google.zxing:javase:3.4.0'

    }

}
