//---------------------------------------------------------------------------------------
// PROJECT

group 'com.simplejcode.commons'
version '1.0'

//---------------------------------------------------------------------------------------
// IDEA

apply plugin: 'idea'

idea {
    module {
        excludeDirs = [file(".gradle"), file(".build"), file(".idea"), file(".out")]
    }
}

//---------------------------------------------------------------------------------------
// ALL

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    version '1.0'

    def sub = projectDir.path.substring(rootProject.projectDir.path.length())
    buildDir = new File(rootProject.projectDir, '.build/' + sub)

    //-----------------------------------------------------------------------------------
    // IDEA

    apply plugin: 'idea'
    idea {
        module {
            outputDir = new File(rootProject.projectDir, '.out')
            testOutputDir = new File(rootProject.projectDir, '.out')
        }
    }

    //-----------------------------------------------------------------------------------
    // JAVA

    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    //-----------------------------------------------------------------------------------
    // PUBLISH

    apply plugin: 'maven-publish'

    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    //-----------------------------------------------------------------------------------
    // DEPENDENCIES

    configurations {

        // test dependencies
        junit

        // jakarta api libraries
        jakartaXmlBind
        jakartaMail
        jakartaServlet
        jakartaEJB
        jakartaWS

        // java ee implementation libraries
        hibernate

        // annotation processor
        lombok

        // third party libraries
        apacheLog4j
        apacheConfiguration
        apachePOI

        googleGson
        googleQrCode
        googleTwoFA

        jsoniter
        pdf

    }

    dependencies {

        junit                          'junit:junit:4.12'

        jakartaXmlBind                 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
        jakartaMail                    'jakarta.mail:jakarta.mail-api:2.1.2'
        jakartaServlet                 'jakarta.servlet:jakarta.servlet-api:6.0.0'
        jakartaEJB                     'jakarta.ejb:jakarta.ejb-api:4.0.1'
        jakartaWS                      'jakarta.websocket:jakarta.websocket-client-api:2.1.1'

        hibernate                      'org.hibernate:hibernate-core:5.3.1.Final'

        lombok                         'org.projectlombok:lombok:1.18.22'
        annotationProcessor           ('org.projectlombok:lombok:1.18.22')

        apacheLog4j                    'org.apache.logging.log4j:log4j:2.11.2'
        apacheLog4j                    'org.apache.logging.log4j:log4j-api:2.11.2'
        apacheLog4j                    'org.apache.logging.log4j:log4j-core:2.11.2'
        apacheConfiguration            'org.apache.commons:commons-configuration2:2.0'
        apachePOI                      'org.apache.poi:poi:4.0.1'
        apachePOI                      'org.apache.poi:poi-ooxml:4.0.1'

        googleGson                     'com.google.code.gson:gson:2.8.6'
        googleQrCode                   'com.google.zxing:core:3.3.0'
        googleQrCode                   'com.google.zxing:javase:3.4.0'
        googleTwoFA                    'com.warrenstrange:googleauth:1.2.0'

        jsoniter                       'com.jsoniter:jsoniter:0.9.23'
        pdf                            'org.apache.pdfbox:pdfbox:2.0.11'

    }

}
